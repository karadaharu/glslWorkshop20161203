<!DOCTYPE html>
<html prefix="og:http://ogp.me/ns#">
  <head>
    <title>2016.12.3</title>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width,initial-scale=1">
    <meta name="keywords" content="keywords,keywords,keywords">
    <meta name="description" content="description description description description">
    <meta property="og:title" content="2016.12.3">
    <meta property="og:description" content="description description description description">
    <meta property="og:url" content="http://sample.com/">
    <meta property="og:type" content="website">
    <meta property="og:image" content="http://sample.com/assets/img/ogp.png">
    <meta name="twitter:card" content="summary_large_image">
    <meta name="twitter:title" content="2016.12.3">
    <meta name="twitter:description" content="description description description description">
    <meta name="twitter:image" content="http://sample.com/assets/img/ogp.png">
    <link rel="shortcut icon" href="/favicon.png">
    <link rel="apple-touch-icon" href="/apple-touch-icon.png">
    <link rel="stylesheet" media="all" href="../assets/css/common.css" type="text/css">
  </head>
  <body class="index">
    <div id="wrapper">
      <div id="loading"></div>
      <div id="contents">
        <section id="main">
          <canvas></canvas>
        </section>
      </div>
    </div>
    <script src="../assets/js/lib/three.js" type="text/javascript"></script>
    <script src="../assets/js/lib/TrackballControls.js" type="text/javascript"></script>
    <script src="../assets/js/lib/TweenMax.js" type="text/javascript"></script>
    <script src="../assets/js/lib/dat.gui.js" type="text/javascript"></script>
    <script src="../assets/js/lib/jquery.js" type="text/javascript"></script>
    <script src="../assets/js/lib/webfontloader.js" type="text/javascript"></script>
    <script src="../assets/js/step5/FloatingCharsGeometry.js" type="text/javascript"></script>
    <script src="../assets/js/step5/FloatingChars.js" type="text/javascript"></script>
    <script src="../assets/js/step5/MainVisual.js" type="text/javascript"></script>
    <script src="../assets/js/step5/index.js" type="text/javascript"></script>
    <script id="vertexShader" type="x-shader/x-vertex">// three.jsが予め定義してるuniform変数
uniform mat4 modelMatrix;  // モデル変換行列
uniform mat4 viewMatrix;  // ビュー変換行列
uniform mat4 projectionMatrix;  // プロジェクション変換行列
uniform vec3 cameraPosition;  // カメラの位置

// 自分で定義したuniform変数
uniform float time;  // 経過時間
uniform float numChars;  // 文字数 (正方形の数)
uniform float numTextureGridRows;  // テクスチャの横方向の文字数
uniform float numTextureGridCols;  // テクスチャの縦方向の文字数
uniform float textureTxtLength;  // テクスチャに使っている文字の数
uniform float animationValue1;  // アニメーション適用度1
uniform float animationValue2;  // アニメーション適用度2
uniform float animationValue3;  // アニメーション適用度3

// FloatingCharsGeometryで追加したattribute
attribute vec3 position;  // 座標
attribute vec3 randomValues;  // ランダム値
attribute vec2 uv;  //UV座標
attribute float charIndex;  // 何番目の文字(正方形に)属するか

// フラグメントシェーダに渡す値
varying vec4 vColor;  // 文字色
varying vec2 vUv;  // UV座標

// 定数としてPIを定義
const float PI = 3.1415926535897932384626433832795;

// 3次元ベクトルを任意の軸で回転
vec3 rotateVec3(vec3 p, float angle, vec3 axis){
  vec3 a = normalize(axis);
  float s = sin(angle);
  float c = cos(angle);
  float r = 1.0 - c;
  mat3 m = mat3(
    a.x * a.x * r + c,
    a.y * a.x * r + a.z * s,
    a.z * a.x * r - a.y * s,
    a.x * a.y * r - a.z * s,
    a.y * a.y * r + c,
    a.z * a.y * r + a.x * s,
    a.x * a.z * r + a.y * s,
    a.y * a.z * r - a.x * s,
    a.z * a.z * r + c
  );
  return m * p;
}

// 範囲を設定し直す
float map(float value, float inputMin, float inputMax, float outputMin, float outputMax, bool clamp) {
  if(clamp == true) {
    if(value < inputMin) return outputMin;
    if(value > inputMax) return outputMax;
  }

  float p = (outputMax - outputMin) / (inputMax - inputMin);
  return ((value - inputMin) * p) + outputMin;
}

// hsvの値をrgbに変換
vec3 hsv2rgb(vec3 c) {
  vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

// 距離から透明度を計算
float getAlpha(float distance) {
  float da = abs(distance - 400.0) / 500.0;
  return clamp(1.0 - da, 0.0, 1.0);
}

// time, scale, offsetを使って角度を返す
// 範囲は -PI ~ PI
float getRad(float scale, float offset) {
  return map(mod(time * scale + offset, PI * 2.0), 0.0, PI * 2.0, -PI, PI, true);
}


// メインの処理
void main() {
  vec3 pos = position;
  float theta;

  // アニメーションの半径
  float radius = 60.0;

  //
  // animation1
  // ビルボード
  //

  pos -= animationValue1 * position;
  theta = getRad(4.0, (randomValues.x + randomValues.y + randomValues.z) * 20.0);
  pos.z += animationValue1 * (radius + radius * map(sin(theta), -1.0, 1.0, 0.0, 1.0, true));
  theta = getRad(4.0, randomValues.x * 20.0);
  pos = rotateVec3(pos, animationValue1 * theta, vec3(0.0, 1.0, 0.0));
  theta = getRad(4.0, randomValues.y * 20.0);
  pos = rotateVec3(pos, animationValue1 * theta, vec3(1.0, 0.0, 0.0));
  theta = getRad(4.0, randomValues.z * 20.0);
  pos = rotateVec3(pos, animationValue1 * theta, vec3(0.0, 0.0, 1.0));

  //
  // animation2
  // 8個のリング状
  //

  float numRings = 8.0;  // リングの個数
  float ringIndex = mod(charIndex, numRings);  // どのリングに属するか
  float numCharsPerRing = numChars / numRings;  // リングごとの文字数

  // 4段階にリングの位置を設定
  pos.y += animationValue2 * map(ringIndex, 0.0, numRings - 1.0, -60.0, 60.0, true);
  pos.z += animationValue2 * radius;  // まず手前にradius分移動

  // y軸を中心にリング状になるように文字を配置
  theta = getRad(10.0, PI * 2.0 / numCharsPerRing * mod((charIndex - ringIndex) / numRings, numCharsPerRing));
  pos = rotateVec3(pos, animationValue2 * theta, vec3(0.0, 1.0, 0.0));

  //
  // animation3
  // 球面上の移動
  //

  pos.z += animationValue3 * radius;
  theta = getRad(6.0, randomValues.x * 10.0);
  pos = rotateVec3(pos, animationValue3 * theta, vec3(0.0, 1.0, 0.0));
  theta = getRad(6.0, randomValues.y * 10.0);
  pos = rotateVec3(pos, animationValue3 * theta, vec3(1.0, 0.0, 0.0));
  theta = getRad(6.0, randomValues.z * 10.0);
  pos = rotateVec3(pos, animationValue3 * theta, vec3(0.0, 0.0, 1.0));

  //
  // フラグメントシェーダにに渡すUV座標を計算
  //

  // テクスチャ上の何番目の文字を使うか
  float txtTextureIndex = mod(charIndex, textureTxtLength);

  // テクスチャの横方向のグリッド単位
  float uUnit = 1.0 / numTextureGridCols;

  // テクスチャの縦方向のグリッド単位
  float vUnit = 1.0 / numTextureGridRows;

  // フラグメントシェーダに渡すUV座標を代入
  vUv = vec2(
    (uv.x + mod(txtTextureIndex, numTextureGridCols)) * uUnit,
    (uv.y + floor(txtTextureIndex / numTextureGridCols)) * vUnit
  );


  //
  // 最終的な座標とフラグメントシェーダに渡す文字色を計算
  //

  // モデル変換
  vec4 modelPos = modelMatrix * vec4(pos, 1.0);

  // ビュー変換
  vec4 modelViewPos = viewMatrix * modelPos;

  // ビルボードのための処理 (animation1)
  modelViewPos += vec4(position, 0.0) * animationValue1;

  // プロジェクション変換した座標をgl_Positionに代入
  gl_Position = projectionMatrix * modelViewPos;

  // カメラからの距離を算出
  float d = distance(cameraPosition, modelPos.xyz);

  // フラグメントシェーダに渡すcolorを計算
  // 時間経過でHSVのHが徐々に変化
  // 距離に応じてalphaの値が変化
  vColor = vec4(hsv2rgb(
    vec3(
      (sin(getRad(2.0, randomValues.x * 2.0)) + 1.0) * 0.5,
      0.8,
      0.8
    )
  ), getAlpha(d));
}

    </script>
    <script id="fragmentShader" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D txtTexture;  // テクスチャ

varying vec4 vColor;  // 文字色
varying vec2 vUv;     // UV座標

// メインの処理
void main(){
  vec4 color = texture2D(txtTexture, vUv) * vColor;
  if(color.a == 0.0) {
    discard;
  } else {
    gl_FragColor = color;
  }
}

    </script>
  </body>
</html>