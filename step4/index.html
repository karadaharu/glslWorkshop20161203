<!DOCTYPE html>
<html prefix="og:http://ogp.me/ns#">
  <head>
    <title>2016.12.3</title>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width,initial-scale=1">
    <meta name="keywords" content="keywords,keywords,keywords">
    <meta name="description" content="description description description description">
    <meta property="og:title" content="2016.12.3">
    <meta property="og:description" content="description description description description">
    <meta property="og:url" content="http://sample.com/">
    <meta property="og:type" content="website">
    <meta property="og:image" content="http://sample.com/assets/img/ogp.png">
    <meta name="twitter:card" content="summary_large_image">
    <meta name="twitter:title" content="2016.12.3">
    <meta name="twitter:description" content="description description description description">
    <meta name="twitter:image" content="http://sample.com/assets/img/ogp.png">
    <link rel="shortcut icon" href="/favicon.png">
    <link rel="apple-touch-icon" href="/apple-touch-icon.png">
    <link rel="stylesheet" media="all" href="../assets/css/common.css" type="text/css">
  </head>
  <body class="index">
    <div id="wrapper">
      <div id="loading"></div>
      <div id="contents">
        <section id="main">
          <canvas></canvas>
        </section>
      </div>
    </div>
    <script src="../assets/js/lib/three.js" type="text/javascript"></script>
    <script src="../assets/js/lib/TrackballControls.js" type="text/javascript"></script>
    <script src="../assets/js/lib/TweenMax.js" type="text/javascript"></script>
    <script src="../assets/js/lib/dat.gui.js" type="text/javascript"></script>
    <script src="../assets/js/lib/jquery.js" type="text/javascript"></script>
    <script src="../assets/js/lib/webfontloader.js" type="text/javascript"></script>
    <script src="../assets/js/step4/FloatingCharsGeometry.js" type="text/javascript"></script>
    <script src="../assets/js/step4/FloatingChars.js" type="text/javascript"></script>
    <script src="../assets/js/step4/MainVisual.js" type="text/javascript"></script>
    <script src="../assets/js/step4/index.js" type="text/javascript"></script>
    <script id="vertexShader" type="x-shader/x-vertex">
uniform mat4 modelViewMatrix;
uniform mat4 modelMatrix;
uniform mat4 projectionMatrix;
uniform float time;
uniform vec3 cameraPosition;
uniform float numChars;
uniform float numTextureGridRows;
uniform float numTextureGridCols;
uniform float textureTxtLength;

uniform float animationValue1;
uniform float animationValue2;
uniform float animationValue3;

attribute vec3 position;
attribute vec3 randomValues;
attribute vec2 uv;
attribute float charIndex;

varying vec4 vColor;
varying vec2 vUv;

const float PI = 3.1415926535897932384626433832795;

const float radius = 60.0;

vec3 rotateVec3(vec3 p, float angle, vec3 axis){
  vec3 a = normalize(axis);
  float s = sin(angle);
  float c = cos(angle);
  float r = 1.0 - c;
  mat3 m = mat3(
    a.x * a.x * r + c,
    a.y * a.x * r + a.z * s,
    a.z * a.x * r - a.y * s,
    a.x * a.y * r - a.z * s,
    a.y * a.y * r + c,
    a.z * a.y * r + a.x * s,
    a.x * a.z * r + a.y * s,
    a.y * a.z * r - a.x * s,
    a.z * a.z * r + c
  );
  return m * p;
}

float map(float value, float inputMin, float inputMax, float outputMin, float outputMax, bool clamp) {
  if(clamp == true) {
    if(value < inputMin) return outputMin;
    if(value > inputMax) return outputMax;
  }

  float p = (outputMax - outputMin) / (inputMax - inputMin);
  return ((value - inputMin) * p) + outputMin;
}

float getRad(float scale, float offset) {
  return map(mod(time * scale + offset, PI * 2.0), 0.0, PI * 2.0, -PI, PI, true);
}

void main() {
  vec3 pos = position;
  float theta;

  //
  // animation1
  //

  // y軸を中心にリング状になるように文字を配置
  pos.z += animationValue1 * radius;
  theta = getRad(10.0, PI * 2.0 / numChars * charIndex);
  pos = rotateVec3(pos, animationValue1 * theta, vec3(0.0, 1.0, 0.0));

  //
  // animation2
  //

  pos.z += animationValue2 * radius;
  theta = getRad(4.0, randomValues.x * 20.0);
  pos = rotateVec3(pos, animationValue2 * theta, vec3(0.0, 1.0, 0.0));
  theta = getRad(4.0, randomValues.y * 20.0);
  pos = rotateVec3(pos, animationValue2 * theta, vec3(1.0, 0.0, 0.0));
  theta = getRad(4.0, randomValues.z * 20.0);
  pos = rotateVec3(pos, animationValue2 * theta, vec3(0.0, 0.0, 1.0));


  //
  // animation3
  //

  pos.x += animationValue3 * 20.0 * (charIndex - (numChars - 1.0) * 0.5);
  theta = getRad(20.0, charIndex * 20.0);
  pos = rotateVec3(pos, animationValue3 * theta, vec3(1.0, 0.0, 0.0));


  gl_Position = projectionMatrix * modelViewMatrix * vec4(pos, 1.0);

  // fragmentShaderに渡すcolorを計算
  vColor = vec4(0.0, 0.0, 0.0, 1.0);

  // fragmentShaderに渡すUV座標を計算
  float txtTextureIndex = mod(charIndex, textureTxtLength);
  float uUnit = 1.0 / numTextureGridCols;
  float vUnit = 1.0 / numTextureGridRows;
  vUv = vec2(
    (uv.x + mod(txtTextureIndex, numTextureGridCols)) * uUnit,
    (uv.y + floor(txtTextureIndex / numTextureGridCols)) * vUnit
  );
}

    </script>
    <script id="fragmentShader" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D txtTexture;
varying vec4 vColor;
varying vec2 vUv;

void main(){
  vec4 color = texture2D(txtTexture, vUv) * vColor;
  if(color.a == 0.0) {
    discard;
  } else {
    gl_FragColor = color;
  }
}

    </script>
  </body>
</html>